apply plugin: 'application'
apply plugin: 'eclipse'
apply plugin: 'java'
apply plugin: 'distribution'

sourceCompatibility = '1.8'
mainClassName = 'com.github.unafraid.telegram.quizbot.Main'

task wrapper(type: Wrapper) {
	gradleVersion = '2.5'
}

repositories {
	mavenCentral()
	maven { url "https://jitpack.io" }
	maven { url "http://maven.admincp.eu/content/repositories/releases/" }
}

dependencies {
	compile(group: 'org.slf4j', name: 'slf4j-api', version: '1.7.12')
	compile(group: 'org.apache.logging.log4j', name: 'log4j-slf4j-impl', version: '2.3')
	compile(group: 'org.apache.logging.log4j', name: 'log4j-core', version: '2.3')
	compile(group: 'com.typesafe', name: 'config', version: '1.3.0')
	compile(group: 'org.l2junity', name: 'commons', version: '1.0.1')
	compile(group: 'com.mchange', name: 'c3p0', version: '0.9.5.1')
	compile(group: 'mysql', name: 'mysql-connector-java', version: '5.1.36')
	compile(group: 'com.github.rubenlagus', name: 'TelegramBots', version: 'v2.3.3.7')
}

tasks.withType(JavaCompile) {
	options.encoding = 'UTF-8'
}

def generalManifest = manifest {
	attributes('Built-By': System.getProperty('user.name'),
		'Implementation-URL': 'https://github.com/UnAfraid/QuizTelegramBot',
		'Class-Path': configurations.runtime.collect({ 'libs/' + it.getName() }).join(' '))
}

jar {
	manifest {
		from(generalManifest)
		attributes('Main-Class' : 'com.github.unafraid.telegram.quizbot.Main')
	}	
}

task stage {
  dependsOn clean, build
}


distributions {
	main {
		contents {
			from('dist')
			into('libs') {
				from(configurations.runtime)
			}
			from(jar)
		}
	}
}

eclipse {
	project {
		natures('org.springsource.ide.eclipse.gradle.core.nature')
	}
}

tasks.eclipse.doLast {
	copy {
		from('eclipse-settings')
		into('.settings')
	}
}

tasks.cleanEclipse.doLast {
	delete('.settings')
}
